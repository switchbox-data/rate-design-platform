# Resolve project root as absolute path from this Justfile's location (works for any clone)
project_root := absolute_path(justfile_directory() / ".." / ".." / ".." / ".." / "..")
state := "RI"
utility := "rie"
start_month := "2025-01"
end_month := "2025-12"
load_year := "2025"
mc_year := "2025"
mc_table_path := project_root + "/rate_design/ri/hp_rates/data/marginal_costs/ri_marginal_costs_2025.csv"
zone_s3_base := "s3://data.sb/eia/hourly_demand/zones/"
utility_s3_base := "s3://data.sb/eia/hourly_demand/utilities/"
mc_output_s3_base := "s3://data.sb/switchbox/marginal_costs/ri/"

create-td-mc-data:
  just create-td-mc-data-rie

create-td-mc-data-rie:
  cd {{project_root}} && uv run python {{project_root}}/utils/fetch_eia_zone_loads.py \
    --state {{state}} \
    --start-month {{start_month}} \
    --end-month {{end_month}} \
    --s3-base {{zone_s3_base}}
  cd {{project_root}} && uv run python {{project_root}}/utils/aggregate_eia_utility_loads.py \
    --state {{state}} \
    --year {{load_year}} \
    --utility {{utility}} \
    --zone-s3-base {{zone_s3_base}} \
    --utility-s3-base {{utility_s3_base}} \
    --upload
  cd {{project_root}} && uv run python {{project_root}}/utils/generate_utility_tx_dx_mc.py \
    --state {{state}} \
    --utility {{utility}} \
    --mc-year {{mc_year}} \
    --load-year {{load_year}} \
    --mc-table-path {{mc_table_path}} \
    --utility-load-s3-base {{utility_s3_base}} \
    --output-s3-base {{mc_output_s3_base}} \
    --upload
