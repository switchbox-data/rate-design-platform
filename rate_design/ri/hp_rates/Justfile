# RI heat pump tasks (placeholder).

# Resolve project root as absolute path using git
project_root := `git rev-parse --show-toplevel`

# -----------------------------------------------------------------------------
# Run RI scenarios
# -----------------------------------------------------------------------------
# Example:
# just run-scenario 1 utility="rie"
run-scenario run_num utility:
  uv run python {{project_root}}/rate_design/ri/hp_rates/run_scenario.py \
    --run-num "{{run_num}}" \
    --utility "{{utility}}"


# -----------------------------------------------------------------------------
# Post-processing
# -----------------------------------------------------------------------------

# Revenue requirement YAML outputs from CAIRO BAT results.
ri_periods_yaml := project_root + "/rate_design/ri/hp_rates/config/periods/rie.yaml"
ri_rev_requirement_dir := project_root + "/rate_design/ri/hp_rates/config/rev_requirement"
ri_default_rev_requirement_yaml := ri_rev_requirement_dir + "/rie.yaml"
ri_differentiated_rev_requirement_yaml := ri_rev_requirement_dir + "/rie_hp_vs_nonhp.yaml"

compute-subclass-rr run_dir scenario_config run_num="1" group_col="has_hp" cross_subsidy_col="BAT_percustomer" differentiated_yaml=ri_differentiated_rev_requirement_yaml default_yaml=ri_default_rev_requirement_yaml:
  just -f {{project_root}}/utils/Justfile compute-subclass-rr \
    "{{run_dir}}" \
    "{{scenario_config}}" \
    "{{differentiated_yaml}}" \
    "{{default_yaml}}" \
    "{{run_num}}" \
    "{{group_col}}" \
    "{{cross_subsidy_col}}"

# Seasonal discount inputs from run outputs + ResStock loads.
compute-seasonal-discount-inputs run_dir resstock_loads_path cross_subsidy_col="BAT_percustomer" output_dir="":
  just -f {{project_root}}/utils/Justfile compute-seasonal-discount-inputs \
    "{{run_dir}}" \
    "{{resstock_loads_path}}" \
    "{{cross_subsidy_col}}" \
    "{{output_dir}}"

# Tariff JSON creation from seasonal_discount_rate_inputs.csv.
create-seasonal-discount-tariff base_tariff_json seasonal_inputs_csv label output_path periods_yaml=ri_periods_yaml:
  just -f {{project_root}}/utils/Justfile create-seasonal-discount-tariff \
    "{{base_tariff_json}}" \
    "{{seasonal_inputs_csv}}" \
    "{{label}}" \
    "{{output_path}}" \
    "{{periods_yaml}}"

# Promote calibrated tariffs from run output:
# reads <run_dir>/tariff_final_config.json, converts each tariff to URDB format,
# and writes one <tariff_key>_calibrated.json per key under config/tariffs/electric.
copy-calibrated-tariff-from-run run_dir state output_dir="":
  just -f {{project_root}}/utils/Justfile copy-calibrated-tariff-from-run \
    "{{run_dir}}" \
    "{{state}}" \
    "{{output_dir}}"

ri_tariffs_electric_dir := project_root + "/rate_design/ri/hp_rates/config/tariffs/electric"

# -----------------------------------------------------------------------------
# PRE: FLAT tariff derivation
# -----------------------------------------------------------------------------
# Create flat and flat_supply tariff and write to config/tariffs/electric/
create-rie-flat-tariff:
  just -f {{project_root}}/utils/Justfile create-flat-tariff \
    "{{ri_tariffs_electric_dir}}/rie_a16.json" \
    "{{ri_tariffs_electric_dir}}/rie_a16_supply.json" \
    "rie_flat" \
    "rie_flat_supply" \
    "{{ri_tariffs_electric_dir}}"

# -----------------------------------------------------------------------------
# PRE: TOU tariff derivation
# -----------------------------------------------------------------------------
# Derive seasonal TOU tariff + derivation spec for RI.
# Uses local ResStock mounts under /data.sb and writes under config/.
ri_td_mc_path := "s3://data.sb/switchbox/marginal_costs/ri/region=isone/utility=rie/year=2025/00000000.parquet"

create-seasonal-tou tou_tariff_key="rie_hp_seasonalTOU" upgrade_id="00" year="2025" tou_base_rate="0.06" tou_fixed_charge="6.75" periods_yaml=ri_periods_yaml td_mc_path=ri_td_mc_path:
  just -f {{project_root}}/utils/Justfile create-seasonal-tou \
    "s3://data.sb/nrel/cambium/2024/scenario=MidCase/t={{year}}/gea=ISONE/r=p133/data.parquet" \
    "RI" \
    "rie" \
    "{{year}}" \
    "{{td_mc_path}}" \
    "/data.sb/nrel/resstock/res_2024_amy2018_2/metadata/state=RI/upgrade={{upgrade_id}}/metadata-sb.parquet" \
    "/data.sb/nrel/resstock/res_2024_amy2018_2/load_curve_hourly/state=RI/upgrade={{upgrade_id}}" \
    "451381" \
    "{{tou_tariff_key}}" \
    "{{project_root}}/rate_design/ri/hp_rates/config" \
    "{{periods_yaml}}" \
    "{{tou_base_rate}}" \
    "{{tou_fixed_charge}}"

# -----------------------------------------------------------------------------
# PRE: Tariff mapping (electric and gas)
# -----------------------------------------------------------------------------
path_s3_resstock_metadata := "s3://data.sb/nrel/resstock/res_2024_amy2018_2/metadata"
path_s3_utility_assignment := "s3://data.sb/nrel/resstock/res_2024_amy2018_2/metadata_utility"
tariff_maps_dir_ri := project_root + "/rate_design/ri/hp_rates/config/tariff_maps"
ri_scenarios_dir := project_root + "/rate_design/ri/hp_rates/config/scenarios"

create-electric-tariff-maps-all:
    just -f {{project_root}}/utils/Justfile write-tariff-maps-all "{{ri_scenarios_dir}}"

create-gas-tariff-map electric_utility upgrade_id output_dir:
  just -f {{project_root}}/utils/Justfile create-gas-tariff-map \
    "{{path_s3_resstock_metadata}}" \
    "{{path_s3_utility_assignment}}" \
    "RI" \
    "{{electric_utility}}" \
    "{{upgrade_id}}" \
    "{{output_dir}}"

create-gas-tariff-map-rie:
  just create-gas-tariff-map rie 00 {{tariff_maps_dir_ri}}/gas

create-gas-tariff-maps-all:
  just create-gas-tariff-map-rie

# -----------------------------------------------------------------------------
# Post-processing: CPI and RI LIDR+ discounts
# -----------------------------------------------------------------------------


# Apply RI LIDR+ discounts to CAIRO bills. Run fetch-cpi first, then pass --cpi-s3-path.
apply-ri-lmi-discounts run_dir state utility fpl_year cpi_s3_path participation_rate="1.0" rider="false":
  uv run python {{project_root}}/utils/post/apply_ri_lmi_discounts_to_bills.py \
    --run-dir "{{run_dir}}" \
    --state {{state}} \
    --utility {{utility}} \
    --fpl-year {{fpl_year}} \
    --cpi-s3-path "{{cpi_s3_path}}" \
    --participation-rate {{participation_rate}} \
    {{ if rider == "true" { "--rider" } else { "" } }} \
    --upload

# -----------------------------------------------------------------------------
# PRE: EIA hourly loads and marginal cost allocation (zone fetch, utility aggregate, MC allocation)
# -----------------------------------------------------------------------------
pre_state := "RI"
pre_utility := "rie"
pre_start_month := "2025-01"
pre_end_month := "2025-12"
pre_load_year := "2025"
pre_mc_year := "2025"
pre_mc_table_path := project_root + "/rate_design/ri/hp_rates/config/marginal_costs/ri_marginal_costs_2025.csv"
pre_zone_s3_base := "s3://data.sb/eia/hourly_demand/zones/"
pre_utility_s3_base := "s3://data.sb/eia/hourly_demand/utilities/"
pre_mc_output_s3_base := "s3://data.sb/switchbox/marginal_costs/ri/"

create-td-mc-data:
  just create-td-mc-data-rie

create-td-mc-data-rie:
  just -f {{project_root}}/data/eia/hourly_loads/Justfile fetch-zone-data {{pre_state}} {{pre_start_month}} {{pre_end_month}}
  just -f {{project_root}}/data/eia/hourly_loads/Justfile aggregate-utility-loads {{pre_state}} {{pre_load_year}} {{pre_utility}}
  uv run python {{project_root}}/utils/pre/generate_utility_tx_dx_mc.py \
    --state {{pre_state}} \
    --utility {{pre_utility}} \
    --mc-year {{pre_mc_year}} \
    --load-year {{pre_load_year}} \
    --mc-table-path {{pre_mc_table_path}} \
    --utility-load-s3-base {{pre_utility_s3_base}} \
    --output-s3-base {{pre_mc_output_s3_base}} \
    --upstream-hours 100 \
    --dist-hours 100 \
    --upload

# =============================================================================
# Create scenario YAMLs from Google Sheets
# =============================================================================

create-scenario-yamls:
    just -f {{project_root}}/utils/Justfile create-scenario-yamls

# =============================================================================
# Orchestrated runs 1–12 (PRE + RUN for each)
# =============================================================================

ri_scenario_config := project_root + "/rate_design/ri/hp_rates/config/scenarios/scenarios_rie.yaml"
latest_output := project_root + "/utils/scripts/latest_run_output.sh"
resstock_loads_00 := "/data.sb/nrel/resstock/res_2024_amy2018_2/load_curve_hourly/state=RI/upgrade=00"

# One-time global pre-processing (scenario YAMLs + tariff maps).
all-pre:
    just create-scenario-yamls
    just create-electric-tariff-maps-all

# Run 1: precalc flat, delivery
run-1:
    just create-rie-flat-tariff
    just run-scenario 1 rie

# Compute differentiated revenue requirements from run-1 BAT output.
# Produces rie_hp_vs_nonhp.yaml needed by runs 5, 6, 9, 10 (2-tariff precalc runs).
compute-rev-requirements:
    #!/usr/bin/env bash
    set -euo pipefail
    run1_dir=$(bash "{{latest_output}}" "{{ri_scenario_config}}" 1)
    echo ">> compute-rev-requirements: resolved run-1 output → ${run1_dir}" >&2
    just compute-subclass-rr "${run1_dir}" "{{ri_scenario_config}}"

# Run 2: precalc flat, supply (flat_supply.json exists from run-1 PRE)
run-2:
    just run-scenario 2 rie

# Run 3: default flat calibrated, delivery (needs run 1 output)
run-3:
    #!/usr/bin/env bash
    set -euo pipefail
    run1_dir=$(bash "{{latest_output}}" "{{ri_scenario_config}}" 1)
    echo ">> run-3: resolved run-1 output → ${run1_dir}" >&2
    just copy-calibrated-tariff-from-run "${run1_dir}" ri
    just run-scenario 3 rie

# Run 4: default flat calibrated, supply (needs run 2 output)
run-4:
    #!/usr/bin/env bash
    set -euo pipefail
    run2_dir=$(bash "{{latest_output}}" "{{ri_scenario_config}}" 2)
    echo ">> run-4: resolved run-2 output → ${run2_dir}" >&2
    just copy-calibrated-tariff-from-run "${run2_dir}" ri
    just run-scenario 4 rie

# Run 5: precalc hp_seasonal vs flat, delivery
# Derives rie_hp_seasonal.json from run 1 seasonal inputs + rie_flat_calibrated.json base.
run-5:
    #!/usr/bin/env bash
    set -euo pipefail
    run1_dir=$(bash "{{latest_output}}" "{{ri_scenario_config}}" 1)
    echo ">> run-5: resolved run-1 output → ${run1_dir}" >&2
    just compute-seasonal-discount-inputs "${run1_dir}" "{{resstock_loads_00}}"
    just create-seasonal-discount-tariff \
        "{{ri_tariffs_electric_dir}}/rie_flat_calibrated.json" \
        "${run1_dir}seasonal_discount_rate_inputs.csv" \
        rie_hp_seasonal \
        "{{ri_tariffs_electric_dir}}/rie_hp_seasonal.json"
    just run-scenario 5 rie

# Run 6: precalc hp_seasonal vs flat, supply
# Derives rie_hp_seasonal_supply.json from run 2 seasonal inputs + rie_flat_supply_calibrated.json base.
run-6:
    #!/usr/bin/env bash
    set -euo pipefail
    run2_dir=$(bash "{{latest_output}}" "{{ri_scenario_config}}" 2)
    echo ">> run-6: resolved run-2 output → ${run2_dir}" >&2
    just compute-seasonal-discount-inputs "${run2_dir}" "{{resstock_loads_00}}"
    just create-seasonal-discount-tariff \
        "{{ri_tariffs_electric_dir}}/rie_flat_supply_calibrated.json" \
        "${run2_dir}seasonal_discount_rate_inputs.csv" \
        rie_hp_seasonal_supply \
        "{{ri_tariffs_electric_dir}}/rie_hp_seasonal_supply.json"
    just run-scenario 6 rie

# Run 7: default hp_seasonal calibrated, delivery (needs run 5 output)
run-7:
    #!/usr/bin/env bash
    set -euo pipefail
    run5_dir=$(bash "{{latest_output}}" "{{ri_scenario_config}}" 5)
    echo ">> run-7: resolved run-5 output → ${run5_dir}" >&2
    just copy-calibrated-tariff-from-run "${run5_dir}" ri
    just run-scenario 7 rie

# Run 8: default hp_seasonal calibrated, supply (needs run 6 output)
run-8:
    #!/usr/bin/env bash
    set -euo pipefail
    run6_dir=$(bash "{{latest_output}}" "{{ri_scenario_config}}" 6)
    echo ">> run-8: resolved run-6 output → ${run6_dir}" >&2
    just copy-calibrated-tariff-from-run "${run6_dir}" ri
    just run-scenario 8 rie

# Run 9: precalc hp_seasonalTOU vs flat, delivery
run-9:
    just create-seasonal-tou rie_hp_seasonalTOU
    just run-scenario 9 rie

# Run 10: precalc hp_seasonalTOU vs flat, supply
run-10:
    just create-seasonal-tou rie_hp_seasonalTOU_supply
    just run-scenario 10 rie

# Run 11: default hp_seasonalTOU calibrated, delivery (needs run 9 output)
run-11:
    #!/usr/bin/env bash
    set -euo pipefail
    run9_dir=$(bash "{{latest_output}}" "{{ri_scenario_config}}" 9)
    echo ">> run-11: resolved run-9 output → ${run9_dir}" >&2
    just copy-calibrated-tariff-from-run "${run9_dir}" ri
    just run-scenario 11 rie

# Run 12: default hp_seasonalTOU calibrated, supply (needs run 10 output)
run-12:
    #!/usr/bin/env bash
    set -euo pipefail
    run10_dir=$(bash "{{latest_output}}" "{{ri_scenario_config}}" 10)
    echo ">> run-12: resolved run-10 output → ${run10_dir}" >&2
    just copy-calibrated-tariff-from-run "${run10_dir}" ri
    just run-scenario 12 rie

# Master recipe: all 12 runs in dependency order.
run-all-sequential:
    just global-pre
    just run-1
    just compute-rev-requirements
    just run-2
    just run-3
    just run-4
    just run-5
    just run-6
    just run-7
    just run-8
    just run-9
    just run-10
    just run-11
    just run-12
