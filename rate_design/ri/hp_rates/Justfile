# RI heat pump tasks (placeholder).

# Resolve project root as absolute path using git
project_root := `git rev-parse --show-toplevel`

# Identify HP customers
identify-hp-customers release upgrade_ids input_filename output_filename:
  #!/usr/bin/env bash
  set -e
  if [[ "{{release}}" != *"2024"* ]]; then
    echo "Warning: this task is only verified to work with 2024 releases. It may work with other releases, but has not been tested."
  fi
  for upgrade_id in {{upgrade_ids}}; do
    uv run python {{project_root}}/utils/identify_hp_customers.py \
      --input_dir "s3://data.sb/nrel/resstock/{{release}}/metadata/state=RI/upgrade=$upgrade_id" \
      --output_dir "s3://data.sb/nrel/resstock/{{release}}/metadata/state=RI/upgrade=$upgrade_id" \
      --input_filename "{{input_filename}}" \
      --output_filename "{{output_filename}}" \
      --upgrade_id "$upgrade_id"
  done

identify-hp-customers-all-upgrades-2024-amy2018-2-ri:
  just identify-hp-customers "res_2024_amy2018_2" "00 01 02 03 04 05" "metadata.parquet" "metadata-sb.parquet"

# Identify heating type
identify-heating-type release upgrade_ids input_filename output_filename:
  #!/usr/bin/env bash
  set -e
  if [[ "{{release}}" != *"2024"* ]]; then
    echo "Warning: this task is only verified to work with 2024 releases. It may work with other releases, but has not been tested."
  fi
  for upgrade_id in {{upgrade_ids}}; do
    uv run python {{project_root}}/utils/identify_heating_type.py \
      --input_dir "s3://data.sb/nrel/resstock/{{release}}/metadata/state=RI/upgrade=$upgrade_id" \
      --output_dir "s3://data.sb/nrel/resstock/{{release}}/metadata/state=RI/upgrade=$upgrade_id" \
      --input_filename "{{input_filename}}" \
      --output_filename "{{output_filename}}" \
      --upgrade_id "$upgrade_id"
  done

identify-heating-type-all-upgrades-2024-amy2018-2-ri:
  just identify-heating-type "res_2024_amy2018_2" "00 01 02 03 04 05" "metadata.parquet" "metadata-sb.parquet"

# Identify HP customers and heating type
identify-hp-customers-and-heating-type-all-upgrades-2024-amy2018-2-ri:
  just identify-hp-customers "res_2024_amy2018_2" "00 01 02 03 04 05" "metadata.parquet" "metadata-sb.parquet"
  just identify-heating-type "res_2024_amy2018_2" "00 01 02 03 04 05" "metadata-sb.parquet" "metadata-sb.parquet"

# -----------------------------------------------------------------------------
# Post-processing: apply RI LIDR+ discounts to CAIRO bills
# -----------------------------------------------------------------------------
# Run fetch-cpi first (from repo root): just fetch-cpi
# Then pass the CPI path from that run (e.g. s3://data.sb/fred/cpi/cpiaucsl_2020_2025_YYYYMMDD.parquet)
apply-ri-lmi-discounts run_dir state utility inflation_year cpi_s3_path participation_rate="1.0" rider="false":
  uv run python {{project_root}}/utils/post/apply_ri_lmi_discounts_to_bills.py \
    --run-dir "{{run_dir}}" \
    --state {{state}} \
    --utility {{utility}} \
    --inflation-year {{inflation_year}} \
    --cpi-s3-path "{{cpi_s3_path}}" \
    --participation-rate {{participation_rate}} \
    --rider {{rider}} \
    --upload
