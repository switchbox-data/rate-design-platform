# RI heat pump tasks (placeholder).

# Resolve project root as absolute path using git
project_root := `git rev-parse --show-toplevel`
ri_run_scenario_config := project_root + "/rate_design/ri/hp_rates/config/scenarios.yaml"

# -----------------------------------------------------------------------------
# Run RI scenarios
# -----------------------------------------------------------------------------
# Example:
# just run-scenario 1
run-scenario run_num scenario_config=ri_run_scenario_config:
  uv run python {{project_root}}/rate_design/ri/hp_rates/run_scenario.py \
    --run-num "{{run_num}}" \
    --scenario-config "{{scenario_config}}"

# Optional output override (if you don't want the YAML output_dir)
run-scenario-with-output run_num output_dir scenario_config=ri_run_scenario_config:
  uv run python {{project_root}}/rate_design/ri/hp_rates/run_scenario.py \
    --run-num "{{run_num}}" \
    --scenario-config "{{scenario_config}}" \
    --output-dir "{{output_dir}}"

# -----------------------------------------------------------------------------
# Post-processing
# -----------------------------------------------------------------------------

# Revenue requirement YAML outputs from CAIRO BAT results.
ri_periods_yaml := project_root + "/rate_design/ri/hp_rates/config/periods/rie.yaml"
ri_rev_requirement_dir := project_root + "/rate_design/ri/hp_rates/config/rev_requirement"
ri_default_rev_requirement_yaml := ri_rev_requirement_dir + "/rie.yaml"
ri_differentiated_rev_requirement_yaml := ri_rev_requirement_dir + "/rie_hp_vs_nonhp.yaml"

compute-subclass-rr run_dir run_num="1" scenario_config=ri_run_scenario_config group_col="has_hp" cross_subsidy_col="BAT_percustomer" differentiated_yaml=ri_differentiated_rev_requirement_yaml default_yaml=ri_default_rev_requirement_yaml:
  uv run python {{project_root}}/utils/post/compute_subclass_rr.py \
    --run-dir "{{run_dir}}" \
    --run-num "{{run_num}}" \
    --scenario-config "{{scenario_config}}" \
    --group-col "{{group_col}}" \
    --cross-subsidy-col "{{cross_subsidy_col}}" \
    --differentiated-yaml-path "{{differentiated_yaml}}" \
    --default-yaml-path "{{default_yaml}}"

# Seasonal discount inputs from run outputs + ResStock loads.
compute-seasonal-discount-inputs run_dir resstock_loads_path cross_subsidy_col="BAT_percustomer" output_dir="":
  uv run python {{project_root}}/utils/post/compute_seasonal_discount_inputs.py \
    --run-dir "{{run_dir}}" \
    --cross-subsidy-col "{{cross_subsidy_col}}" \
    --resstock-loads-path "{{resstock_loads_path}}" \
    {{ if output_dir != "" { "--output-dir \"" + output_dir + "\"" } else { "" } }}

# Tariff JSON creation from seasonal_discount_rate_inputs.csv.
create-seasonal-discount-tariff base_tariff_json seasonal_inputs_csv label output_path periods_yaml=ri_periods_yaml:
  uv run python {{project_root}}/utils/pre/create_seasonal_discount_tariff.py \
    --base-tariff-json "{{base_tariff_json}}" \
    --seasonal-inputs-csv "{{seasonal_inputs_csv}}" \
    --label "{{label}}" \
    --periods-yaml "{{periods_yaml}}" \
    --output-path "{{output_path}}"

ri_tariffs_electric_dir := project_root + "/rate_design/ri/hp_rates/config/tariffs/electric"
ri_hp_seasonal_calibrated_tariff := ri_tariffs_electric_dir + "/rie_hp_seasonal_calibrated.json"
ri_hp_seasonal_calibrated_supply_tariff := ri_tariffs_electric_dir + "/rie_hp_seasonal_calibrated_supply.json"

# Run 1 (delivery-only) calibrated seasonal tariff.
create-rie-hp-seasonal-calibrated seasonal_inputs_csv:
  just create-seasonal-discount-tariff \
    {{ri_tariffs_electric_dir}}/rie_a16.json \
    "{{seasonal_inputs_csv}}" \
    rie_hp_seasonal_calibrated \
    {{ri_hp_seasonal_calibrated_tariff}}

# Run 2 (delivery + supply) calibrated seasonal tariff.
create-rie-hp-seasonal-calibrated-supply seasonal_inputs_csv:
  just create-seasonal-discount-tariff \
    {{ri_tariffs_electric_dir}}/rie_a16_supply_adj.json \
    "{{seasonal_inputs_csv}}" \
    rie_hp_seasonal_calibrated_supply \
    {{ri_hp_seasonal_calibrated_supply_tariff}}

# -----------------------------------------------------------------------------
# PRE: TOU tariff derivation
# -----------------------------------------------------------------------------
# Derive seasonal TOU tariff + map + derivation spec for RI.
# Uses local ResStock mounts under /data.sb and writes under config/.
ri_td_mc_path := "s3://data.sb/switchbox/marginal_costs/ri/region=isone/utility=rie/year=2025/0000000.parquet"

create-seasonal-tou tou_tariff_key="rie_hp_seasonalTOU" flat_tariff_key="rie_a16" upgrade_id="00" year="2025" tou_base_rate="0.06" tou_fixed_charge="6.75" periods_yaml=ri_periods_yaml td_mc_path=ri_td_mc_path:
  uv run python {{project_root}}/utils/pre/derive_seasonal_tou.py \
    --cambium-path "s3://data.sb/nrel/cambium/2024/scenario=MidCase/t={{year}}/gea=ISONE/r=p133/data.parquet" \
    --state RI \
    --utility rie \
    --year "{{year}}" \
    --path-td-marginal-costs "{{td_mc_path}}" \
    --resstock-metadata-path "/data.sb/nrel/resstock/res_2024_amy2018_2/metadata/state=RI/upgrade={{upgrade_id}}/metadata-sb.parquet" \
    --resstock-loads-path "/data.sb/nrel/resstock/res_2024_amy2018_2/load_curve_hourly/state=RI/upgrade={{upgrade_id}}" \
    --customer-count 451381 \
    --tou-base-rate "{{tou_base_rate}}" \
    --tou-fixed-charge "{{tou_fixed_charge}}" \
    --periods-yaml "{{periods_yaml}}" \
    --tou-tariff-key "{{tou_tariff_key}}" \
    --flat-tariff-key "{{flat_tariff_key}}" \
    --output-dir "{{project_root}}/rate_design/ri/hp_rates/config"

# -----------------------------------------------------------------------------
# PRE: Tariff mapping (electric and gas)
# -----------------------------------------------------------------------------
path_s3_resstock_metadata := "s3://data.sb/nrel/resstock/res_2024_amy2018_2/metadata"
path_s3_utility_assignment := "s3://data.sb/nrel/resstock/res_2024_amy2018_2/metadata_utility"
tariff_maps_dir_ri := project_root + "/rate_design/ri/hp_rates/config/tariff_maps"

map-electric-tariff electric_utility SB_scenario_type SB_scenario_year upgrade_id output_dir:
  uv run python {{project_root}}/utils/pre/electric_tariff_mapper.py \
    --metadata_path "{{path_s3_resstock_metadata}}" \
    --state RI \
    --upgrade_id "{{upgrade_id}}" \
    --electric_utility "{{electric_utility}}" \
    --SB_scenario_type "{{SB_scenario_type}}" \
    --SB_scenario_year "{{SB_scenario_year}}" \
    --output_dir "{{output_dir}}"

map-electric-coned-default:
  just map-electric-tariff coned default 1 00 {{tariff_maps_dir_ri}}/electric

map-electric-coned-seasonal:
  just map-electric-tariff coned seasonal 1 00 {{tariff_maps_dir_ri}}/electric

map-electric-coned-class-specific-seasonal:
  just map-electric-tariff coned class_specific_seasonal 1 00 {{tariff_maps_dir_ri}}/electric

map-electric-national-grid-default:
  just map-electric-tariff nimo default 1 00 {{tariff_maps_dir_ri}}/electric

map-electric-national-grid-seasonal:
  just map-electric-tariff nimo seasonal 1 00 {{tariff_maps_dir_ri}}/electric

map-electric-national-grid-class-specific-seasonal:
  just map-electric-tariff nimo class_specific_seasonal 1 00 {{tariff_maps_dir_ri}}/electric

map-electric-nyseg-default:
  just map-electric-tariff nyseg default 1 00 {{tariff_maps_dir_ri}}/electric

map-electric-nyseg-seasonal:
  just map-electric-tariff nyseg seasonal 1 00 {{tariff_maps_dir_ri}}/electric

map-electric-nyseg-class-specific-seasonal:
  just map-electric-tariff nyseg class_specific_seasonal 1 00 {{tariff_maps_dir_ri}}/electric

map-electric-rie-default:
  just map-electric-tariff rie default 1 00 {{tariff_maps_dir_ri}}/electric

# Generate TOU tariff map: HP customers -> TOU, non-HP -> flat.
# Requires ResStock metadata on S3 (run resstock-identify-hp-customers first).
map-electric-hp-tou tou_tariff_key flat_tariff_key upgrade_id="00":
  uv run python -m utils.pre.compute_tou \
    --metadata-path "{{path_s3_resstock_metadata}}/state=RI/upgrade={{upgrade_id}}/metadata-sb.parquet" \
    --tou-tariff-key "{{tou_tariff_key}}" \
    --flat-tariff-key "{{flat_tariff_key}}" \
    --output-dir "{{tariff_maps_dir_ri}}/electric"

map-electric-rie-seasonal:
  just map-electric-tariff rie seasonal 1 00 {{tariff_maps_dir_ri}}/electric

map-electric-rie-seasonal-discount:
  just map-electric-tariff rie seasonal_discount 1 00 {{tariff_maps_dir_ri}}/electric

map-gas-tariff electric_utility upgrade_id output_dir:
  uv run python {{project_root}}/utils/pre/gas_tariff_mapper.py \
    --metadata_path "{{path_s3_resstock_metadata}}" \
    --utility_assignment_path "{{path_s3_utility_assignment}}" \
    --state RI \
    --upgrade_id "{{upgrade_id}}" \
    --electric_utility "{{electric_utility}}" \
    --output_dir "{{output_dir}}"

map-gas-rie:
  just map-gas-tariff rie 00 {{tariff_maps_dir_ri}}/gas

map-gas-all-utilities:
  just map-gas-rie

# -----------------------------------------------------------------------------
# Post-processing: CPI and RI LIDR+ discounts
# -----------------------------------------------------------------------------
# Delegates to data/fred/cpi/Justfile.
fetch-cpi series="CPIAUCSL" start_year="2020" end_year="2025":
  just -f {{project_root}}/data/fred/cpi/Justfile fetch-cpi {{series}} {{start_year}} {{end_year}}

# Apply RI LIDR+ discounts to CAIRO bills. Run fetch-cpi first, then pass --cpi-s3-path.
apply-ri-lmi-discounts run_dir state utility fpl_year cpi_s3_path participation_rate="1.0" rider="false":
  uv run python {{project_root}}/utils/post/apply_ri_lmi_discounts_to_bills.py \
    --run-dir "{{run_dir}}" \
    --state {{state}} \
    --utility {{utility}} \
    --fpl-year {{fpl_year}} \
    --cpi-s3-path "{{cpi_s3_path}}" \
    --participation-rate {{participation_rate}} \
    {{ if rider == "true" { "--rider" } else { "" } }} \
    --upload

# -----------------------------------------------------------------------------
# PRE: EIA hourly loads and marginal cost allocation (zone fetch, utility aggregate, MC allocation)
# -----------------------------------------------------------------------------
pre_state := "RI"
pre_utility := "rie"
pre_start_month := "2025-01"
pre_end_month := "2025-12"
pre_load_year := "2025"
pre_mc_year := "2025"
pre_mc_table_path := project_root + "/rate_design/ri/hp_rates/config/marginal_costs/ri_marginal_costs_2025.csv"
pre_zone_s3_base := "s3://data.sb/eia/hourly_demand/zones/"
pre_utility_s3_base := "s3://data.sb/eia/hourly_demand/utilities/"
pre_mc_output_s3_base := "s3://data.sb/switchbox/marginal_costs/ri/"

create-td-mc-data:
  just create-td-mc-data-rie

create-td-mc-data-rie:
  just -f {{project_root}}/data/eia/hourly_loads/Justfile fetch-zone-data {{pre_state}} {{pre_start_month}} {{pre_end_month}}
  just -f {{project_root}}/data/eia/hourly_loads/Justfile aggregate-utility-loads {{pre_state}} {{pre_load_year}} {{pre_utility}}
  cd {{project_root}} && uv run python {{project_root}}/utils/pre/generate_utility_tx_dx_mc.py \
    --state {{pre_state}} \
    --utility {{pre_utility}} \
    --mc-year {{pre_mc_year}} \
    --load-year {{pre_load_year}} \
    --mc-table-path {{pre_mc_table_path}} \
    --utility-load-s3-base {{pre_utility_s3_base}} \
    --output-s3-base {{pre_mc_output_s3_base}} \
    --upload

# =============================================================================
# Create scenario YAMLs from Google Sheets
# =============================================================================

# Create scenario YAMLs from the Runs & Charts Google Sheet (requires G_* env and browser auth)
# Writes rate_design/<state>/hp_rates/config/scenarios_<utility>.yaml per utility
create-scenario-yamls dry_run="":
    uv run python {{project_root}}/utils/pre/create_scenario_yamls.py {{ dry_run }}
