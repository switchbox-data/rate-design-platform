# Heat pump rate design tasks.
# Parameterized for multi-state/utility reuse.

# =============================================================================
# Tier 1: Identity (override via env vars)
# =============================================================================

state       := env_var_or_default('STATE', 'ri')
state_upper := uppercase(state)
utility     := env_var_or_default('UTILITY', 'rie')

# =============================================================================
# Tier 2: Utility-specific config
# =============================================================================

region            := "isone"
cambium_ba        := "p133"
year              := "2025"
upgrade           := "00"
default_tariff        := "rie_a16.json"
default_tariff_supply := "rie_a16_supply.json"
upstream_hours    := "100"
dist_hours        := "100"

# =============================================================================
# Tier 3: Derived paths (all computed from Tier 1 + 2)
# =============================================================================

path_repo                           := `git rev-parse --show-toplevel`
path_project                        := path_repo / "rate_design" / state / "hp_rates"
path_config                         := path_project / "config"
path_tariffs_electric               := path_config / "tariffs/electric"
path_tariffs_gas                    := path_config / "tariffs/gas"
path_rateacuity_yaml                := path_tariffs_gas / "rateacuity_tariffs.yaml"
path_scenarios                      := path_config / "scenarios"
path_scenario_config                := path_scenarios / "scenarios_" + utility + ".yaml"
path_periods_yaml                   := path_config / "periods" / utility + ".yaml"
path_rev_requirement                := path_config / "rev_requirement"
path_default_rev_requirement        := path_rev_requirement / utility + ".yaml"
path_differentiated_rev_requirement := path_rev_requirement / utility + "_hp_vs_nonhp.yaml"
path_tariff_maps                    := path_config / "tariff_maps"
path_td_mc                          := "s3://data.sb/switchbox/marginal_costs/" + state + "/region=" + region + "/utility=" + utility + "/year=" + year + "/00000000.parquet"
path_resstock_base                  := "/data.sb/nrel/resstock/res_2024_amy2018_2"
path_s3_resstock_metadata           := "s3://data.sb/nrel/resstock/res_2024_amy2018_2/metadata"
path_s3_utility_assignment          := "s3://data.sb/nrel/resstock/res_2024_amy2018_2/metadata_utility"
path_resstock_loads_00              := path_resstock_base + "/load_curve_hourly/state=" + state_upper + "/upgrade=" + upgrade
path_cambium                        := "s3://data.sb/nrel/cambium/2024/scenario=MidCase/t=" + year + "/gea=" + uppercase(region) + "/r=" + cambium_ba + "/data.parquet"
path_mc_table                       := path_config / "marginal_costs" / state + "_marginal_costs_" + year + ".csv"
path_s3_mc_output                   := "s3://data.sb/switchbox/marginal_costs/" + state + "/"
path_s3_utility_loads               := "s3://data.sb/eia/hourly_demand/utilities/"
path_electric_utility_stats         := "s3://data.sb/eia/861/electric_utility_stats/year=2024/state=" + state_upper + "/data.parquet"
path_default_tariff                 := path_tariffs_electric / default_tariff
path_default_supply_tariff          := path_tariffs_electric / default_tariff_supply
tou_tariff_key                      := utility + "_hp_seasonalTOU"
latest_output                       := path_repo / "utils/mid/latest_run_output.sh"

# =============================================================================
# VALIDATION
# =============================================================================

# Check Justfile vars against scenario YAML (run 1 + run 2 as canonical).
# Default: warn on mismatch. Pass "strict" to exit non-zero (for CI).
validate-config mode="warn":
  uv run python {{path_repo}}/utils/pre/validate_config.py \
    --scenario-config "{{path_scenario_config}}" \
    --state "{{state_upper}}" \
    --utility "{{utility}}" \
    --upgrade "{{upgrade}}" \
    --year "{{year}}" \
    --path-td-mc "{{path_td_mc}}" \
    --path-cambium "{{path_cambium}}" \
    --path-electric-utility-stats "{{path_electric_utility_stats}}" \
    --path-resstock-loads "{{path_resstock_loads_00}}" \
    {{ if mode == "strict" { "--strict" } else { "" } }}

# =============================================================================
# PRE-CONFIG: generate before any runs
# =============================================================================

# Scenario YAMLs from Google Sheets.
create-scenario-yamls:
    just -f {{path_repo}}/utils/Justfile create-scenario-yamls

# Electric tariff map CSVs from scenario YAMLs.
create-electric-tariff-maps-all:
    just -f {{path_repo}}/utils/Justfile write-tariff-maps-all "{{path_scenarios}}"

# Gas tariff maps.
fetch-gas-tariffs:
  uv run python {{path_repo}}/utils/pre/fetch_gas_tariffs_rateacuity.py "{{path_rateacuity_yaml}}" "{{path_tariffs_gas}}"

create-gas-tariff-map electric_utility upgrade_id output_dir:
  just -f {{path_repo}}/utils/Justfile create-gas-tariff-map \
    "{{path_s3_resstock_metadata}}" \
    "{{path_s3_utility_assignment}}" \
    "{{state_upper}}" \
    "{{electric_utility}}" \
    "{{upgrade_id}}" \
    "{{output_dir}}"

create-gas-tariff-maps-all:
  just create-gas-tariff-map {{utility}} {{upgrade}} {{path_tariff_maps}}/gas

# Flat tariff JSONs.
create-flat-tariff:
  just -f {{path_repo}}/utils/Justfile create-flat-tariff \
    "{{path_default_tariff}}" \
    "{{path_default_supply_tariff}}" \
    "{{utility}}_flat" \
    "{{utility}}_flat_supply" \
    "{{path_tariffs_electric}}"

# Seasonal TOU tariff + derivation spec.
# reference_tariff: path to a calibrated tariff JSON whose base rate and fixed
# charge are extracted to set TOU price levels. When empty, derive_seasonal_tou
# falls back to its built-in defaults.
create-seasonal-tou key=tou_tariff_key reference_tariff="":
  just -f {{path_repo}}/utils/Justfile create-seasonal-tou \
    "{{path_cambium}}" \
    "{{state_upper}}" \
    "{{utility}}" \
    "{{year}}" \
    "{{path_td_mc}}" \
    "{{path_resstock_base}}/metadata/state={{state_upper}}/upgrade={{upgrade}}/metadata-sb.parquet" \
    "{{path_resstock_loads_00}}" \
    "{{path_electric_utility_stats}}" \
    "{{key}}" \
    "{{path_config}}" \
    "{{path_periods_yaml}}" \
    "{{reference_tariff}}"

# Marginal cost allocation (state-specific).
create-td-mc-data:
  just create-td-mc-data-rie

create-td-mc-data-rie:
  uv run python {{path_repo}}/utils/pre/generate_utility_tx_dx_mc.py \
    --state {{state_upper}} \
    --utility {{utility}} \
    --mc-year {{year}} \
    --load-year {{year}} \
    --mc-table-path {{path_mc_table}} \
    --utility-load-s3-base {{path_s3_utility_loads}} \
    --output-s3-base {{path_s3_mc_output}} \
    --upstream-hours {{upstream_hours}} \
    --dist-hours {{dist_hours}} \
    --upload

# =============================================================================
# MID-CONFIG: generate between runs (using outputs from earlier runs)
# =============================================================================

# Subclass revenue requirements from CAIRO BAT results.
compute-subclass-rr run_dir scenario_config run_num="1" group_col="has_hp" cross_subsidy_col="BAT_percustomer" differentiated_yaml=path_differentiated_rev_requirement default_yaml=path_default_rev_requirement:
  just -f {{path_repo}}/utils/Justfile compute-subclass-rr \
    "{{run_dir}}" \
    "{{scenario_config}}" \
    "{{differentiated_yaml}}" \
    "{{default_yaml}}" \
    "{{run_num}}" \
    "{{group_col}}" \
    "{{cross_subsidy_col}}"

# Seasonal discount inputs from run outputs + ResStock loads.
compute-seasonal-discount-inputs run_dir resstock_loads_path cross_subsidy_col="BAT_percustomer" output_dir="":
  just -f {{path_repo}}/utils/Justfile compute-seasonal-discount-inputs \
    "{{run_dir}}" \
    "{{resstock_loads_path}}" \
    "{{cross_subsidy_col}}" \
    "{{output_dir}}"

# Seasonal discount tariff JSON from seasonal_discount_rate_inputs.csv.
create-seasonal-discount-tariff base_tariff_json seasonal_inputs_csv label output_path periods_yaml=path_periods_yaml:
  just -f {{path_repo}}/utils/Justfile create-seasonal-discount-tariff \
    "{{base_tariff_json}}" \
    "{{seasonal_inputs_csv}}" \
    "{{label}}" \
    "{{output_path}}" \
    "{{periods_yaml}}"

# Promote calibrated tariffs from a completed run's output.
copy-calibrated-tariff-from-run run_dir output_dir="":
  just -f {{path_repo}}/utils/Justfile copy-calibrated-tariff-from-run \
    "{{run_dir}}" \
    "{{state}}" \
    "{{output_dir}}"

# Differentiated revenue requirements from run-1 BAT output.
# Produces differentiated rev requirement YAML needed by runs 5, 6, 9, 10 (2-tariff precalc runs).
compute-rev-requirements:
    #!/usr/bin/env bash
    set -euo pipefail
    run1_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 1)
    echo ">> compute-rev-requirements: resolved run-1 output → ${run1_dir}" >&2
    just compute-subclass-rr "${run1_dir}" "{{path_scenario_config}}"

# =============================================================================
# RUNS: scenario execution (run-1 through run-12)
# =============================================================================

run-scenario run_num:
  uv run python {{path_project}}/run_scenario.py \
    --run-num "{{run_num}}" \
    --utility "{{utility}}" \
    {{ if env_var_or_default('RDP_NUM_WORKERS', '') != '' { "--num-workers " + env_var_or_default('RDP_NUM_WORKERS', '') } else { "" } }}

# Run 1: precalc flat, delivery
run-1:
    just create-flat-tariff
    just run-scenario 1

# Run 2: precalc flat, supply (flat_supply.json exists from run-1 PRE)
run-2:
    just run-scenario 2

# Run 3: default flat calibrated, delivery (needs run 1 output)
run-3:
    #!/usr/bin/env bash
    set -euo pipefail
    run1_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 1)
    echo ">> run-3: resolved run-1 output → ${run1_dir}" >&2
    just copy-calibrated-tariff-from-run "${run1_dir}"
    just run-scenario 3

# Run 4: default flat calibrated, supply (needs run 2 output)
run-4:
    #!/usr/bin/env bash
    set -euo pipefail
    run2_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 2)
    echo ">> run-4: resolved run-2 output → ${run2_dir}" >&2
    just copy-calibrated-tariff-from-run "${run2_dir}"
    just run-scenario 4

# Run 5: precalc hp_seasonal vs flat, delivery
# Derives hp_seasonal tariff from run 1 seasonal inputs + flat_calibrated base.
run-5:
    #!/usr/bin/env bash
    set -euo pipefail
    run1_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 1)
    echo ">> run-5: resolved run-1 output → ${run1_dir}" >&2
    just compute-seasonal-discount-inputs "${run1_dir}" "{{path_resstock_loads_00}}"
    just create-seasonal-discount-tariff \
        "{{path_tariffs_electric}}/{{utility}}_flat_calibrated.json" \
        "${run1_dir}seasonal_discount_rate_inputs.csv" \
        {{utility}}_hp_seasonal \
        "{{path_tariffs_electric}}/{{utility}}_hp_seasonal.json"
    just run-scenario 5

# Run 6: precalc hp_seasonal vs flat, supply
# Derives hp_seasonal_supply tariff from run 2 seasonal inputs + flat_supply_calibrated base.
run-6:
    #!/usr/bin/env bash
    set -euo pipefail
    run2_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 2)
    echo ">> run-6: resolved run-2 output → ${run2_dir}" >&2
    just compute-seasonal-discount-inputs "${run2_dir}" "{{path_resstock_loads_00}}"
    just create-seasonal-discount-tariff \
        "{{path_tariffs_electric}}/{{utility}}_flat_supply_calibrated.json" \
        "${run2_dir}seasonal_discount_rate_inputs.csv" \
        {{utility}}_hp_seasonal_supply \
        "{{path_tariffs_electric}}/{{utility}}_hp_seasonal_supply.json"
    just run-scenario 6

# Run 7: default hp_seasonal calibrated, delivery (needs run 5 output)
run-7:
    #!/usr/bin/env bash
    set -euo pipefail
    run5_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 5)
    echo ">> run-7: resolved run-5 output → ${run5_dir}" >&2
    just copy-calibrated-tariff-from-run "${run5_dir}"
    just run-scenario 7

# Run 8: default hp_seasonal calibrated, supply (needs run 6 output)
run-8:
    #!/usr/bin/env bash
    set -euo pipefail
    run6_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 6)
    echo ">> run-8: resolved run-6 output → ${run6_dir}" >&2
    just copy-calibrated-tariff-from-run "${run6_dir}"
    just run-scenario 8

# Run 9: precalc hp_seasonalTOU vs flat, delivery
# Depends on run-1 calibrated tariff as reference for TOU price levels.
run-9:
    #!/usr/bin/env bash
    set -euo pipefail
    ref_tariff="{{path_tariffs_electric}}/{{utility}}_flat_calibrated.json"
    if [ ! -f "${ref_tariff}" ]; then
        run1_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 1)
        echo ">> run-9: calibrated tariff missing, copying from run-1 → ${run1_dir}" >&2
        just copy-calibrated-tariff-from-run "${run1_dir}"
    fi
    just create-seasonal-tou {{tou_tariff_key}} "${ref_tariff}"
    just run-scenario 9

# Run 10: precalc hp_seasonalTOU vs flat, supply
# Depends on run-2 calibrated tariff as reference for TOU price levels.
run-10:
    #!/usr/bin/env bash
    set -euo pipefail
    ref_tariff="{{path_tariffs_electric}}/{{utility}}_flat_supply_calibrated.json"
    if [ ! -f "${ref_tariff}" ]; then
        run2_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 2)
        echo ">> run-10: calibrated supply tariff missing, copying from run-2 → ${run2_dir}" >&2
        just copy-calibrated-tariff-from-run "${run2_dir}"
    fi
    just create-seasonal-tou {{tou_tariff_key}}_supply "${ref_tariff}"
    just run-scenario 10

# Run 11: default hp_seasonalTOU calibrated, delivery (needs run 9 output)
run-11:
    #!/usr/bin/env bash
    set -euo pipefail
    run9_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 9)
    echo ">> run-11: resolved run-9 output → ${run9_dir}" >&2
    just copy-calibrated-tariff-from-run "${run9_dir}"
    just run-scenario 11

# Run 12: default hp_seasonalTOU calibrated, supply (needs run 10 output)
run-12:
    #!/usr/bin/env bash
    set -euo pipefail
    run10_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 10)
    echo ">> run-12: resolved run-10 output → ${run10_dir}" >&2
    just copy-calibrated-tariff-from-run "${run10_dir}"
    just run-scenario 12

# Master recipe: all 12 runs in dependency order.

# One-time pre-processing (scenario YAMLs + tariff maps). Validate config checks select inputs in Justfile match scenario YAML.
all-pre:
    just create-scenario-yamls
    just create-electric-tariff-maps-all
    just validate-config

run-all-sequential:
    just run-1
    just compute-rev-requirements
    just run-2
    just run-3
    just run-4
    just run-5
    just run-6
    just run-7
    just run-8
    just run-9
    just run-10
    just run-11
    just run-12

# Run all 12 scenarios as 6 delivery/supply waves in parallel.
# Each wave runs the odd (delivery) and even (supply) run concurrently, each with
# half the available CPUs. Requires --num-workers support in run_scenario.py.
# Use when T4/T8 ratio r < 1.8 (measured in cairo_parallelism_and_workers.md).
run-all-parallel-tracks:
    #!/usr/bin/env bash
    set -euo pipefail
    WORKERS=$(( $(nproc) / 2 ))
    [ "$WORKERS" -lt 1 ] && WORKERS=1
    export RDP_NUM_WORKERS="$WORKERS"
    echo ">> run-all-parallel-tracks: ${WORKERS} workers/track on $(nproc) total vCPUs" >&2

    # Wave 1: run-1 (delivery) and run-2 (supply) are independent precalc runs
    just run-1 & PID1=$!
    just run-2 & PID2=$!
    wait $PID1 || { echo "ERROR: run-1 failed" >&2; exit 1; }
    wait $PID2 || { echo "ERROR: run-2 failed" >&2; exit 1; }

    # compute-rev-requirements uses run-1 BAT output; needed by runs 5, 6, 9, 10
    just compute-rev-requirements

    # Wave 2: run-3 (copies calibrated tariff from run-1) and run-4 (from run-2)
    just run-3 & PID3=$!
    just run-4 & PID4=$!
    wait $PID3 || { echo "ERROR: run-3 failed" >&2; exit 1; }
    wait $PID4 || { echo "ERROR: run-4 failed" >&2; exit 1; }

    # Wave 3: run-5 (delivery hp_seasonal) and run-6 (supply hp_seasonal)
    just run-5 & PID5=$!
    just run-6 & PID6=$!
    wait $PID5 || { echo "ERROR: run-5 failed" >&2; exit 1; }
    wait $PID6 || { echo "ERROR: run-6 failed" >&2; exit 1; }

    # Wave 4
    just run-7 & PID7=$!
    just run-8 & PID8=$!
    wait $PID7 || { echo "ERROR: run-7 failed" >&2; exit 1; }
    wait $PID8 || { echo "ERROR: run-8 failed" >&2; exit 1; }

    # Wave 5: run-9 (delivery TOU) and run-10 (supply TOU)
    just run-9 & PID9=$!
    just run-10 & PID10=$!
    wait $PID9 || { echo "ERROR: run-9 failed" >&2; exit 1; }
    wait $PID10 || { echo "ERROR: run-10 failed" >&2; exit 1; }

    # Wave 6
    just run-11 & PID11=$!
    just run-12 & PID12=$!
    wait $PID11 || { echo "ERROR: run-11 failed" >&2; exit 1; }
    wait $PID12 || { echo "ERROR: run-12 failed" >&2; exit 1; }

    echo ">> run-all-parallel-tracks: all 12 runs complete" >&2

# =============================================================================
# POST: after all runs are completed
# =============================================================================

# Apply RI LIDR+ discounts to CAIRO bills. Run fetch-cpi first, then pass --cpi-s3-path.
apply-ri-lmi-discounts run_dir run_state run_utility fpl_year cpi_s3_path participation_rate="1.0" rider="false":
  uv run python {{path_repo}}/utils/post/apply_ri_lmi_discounts_to_bills.py \
    --run-dir "{{run_dir}}" \
    --state {{run_state}} \
    --utility {{run_utility}} \
    --fpl-year {{fpl_year}} \
    --cpi-s3-path "{{cpi_s3_path}}" \
    --participation-rate {{participation_rate}} \
    {{ if rider == "true" { "--rider" } else { "" } }} \
    --upload
