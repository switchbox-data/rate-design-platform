# NY heat pump rate design tasks.
# Parameterized for multi-utility reuse via env vars.
# See context/tools/run_orchestration.md for full documentation.

# Usage (from repo root):
#   UTILITY=coned just -f rate_design/ny/hp_rates/Justfile run-all-sequential
# Or from rate_design/ny/hp_rates:
#   UTILITY=coned just run-all-sequential

# =============================================================================
# Tier 1: Identity (override via env vars)
# =============================================================================

state       := env_var_or_default('STATE', 'ny')
state_upper := uppercase(state)
utility     := env_var_or_default('UTILITY', 'nyseg')

# =============================================================================
# Tier 2: Utility-specific config (override via env vars if needed)
# =============================================================================
# Most values are uniform across NY utilities or derived from `utility`.
# Just pass UTILITY=<name> to switch utilities:
#   UTILITY=coned just run-1

region                := env_var_or_default('REGION', 'nyiso')
year                  := env_var_or_default('YEAR', '2025')
mc_year               := env_var_or_default('MC_YEAR', '2026')
upgrade               := env_var_or_default('UPGRADE', '00')
default_tariff        := env_var_or_default('DEFAULT_TARIFF', utility + '_default.json')
default_tariff_supply := env_var_or_default('DEFAULT_TARIFF_SUPPLY', utility + '_default.json')
upstream_hours        := env_var_or_default('UPSTREAM_HOURS', '100')
dist_hours            := env_var_or_default('DIST_HOURS', '100')

# =============================================================================
# Tier 3: Derived paths (all computed from Tier 1 + 2)
# =============================================================================

path_repo                           := `git rev-parse --show-toplevel`
project_root                        := path_repo
path_project                        := path_repo / "rate_design" / state / "hp_rates"
path_config                         := path_project / "config"
path_tariffs_electric               := path_config / "tariffs/electric"
path_tariffs_gas                    := path_config / "tariffs/gas"
path_scenarios                      := path_config / "scenarios"
path_scenario_config                := path_scenarios / "scenarios_" + utility + ".yaml"
path_periods_yaml                   := path_config / "periods" / utility + ".yaml"
path_rev_requirement                := path_config / "rev_requirement"
path_default_rev_requirement        := path_rev_requirement / utility + ".yaml"
path_differentiated_rev_requirement := path_rev_requirement / utility + "_hp_vs_nonhp.yaml"
path_revenue_csv                    := "s3://data.sb/switchbox/revenue_requirements/" + state + "/revenue_requirements_dummy.csv"
revenue_utility_col                 := "utility"
revenue_year_col                    := "year"
path_tariff_maps                    := path_config / "tariff_maps"
# TODO: replace cenhud placeholder with per-utility MC data once generated
path_td_mc                          := "s3://data.sb/switchbox/marginal_costs/" + state + "/region=" + region + "/utility=cenhud/year=" + mc_year + "/00000000.parquet"
path_resstock_release               := "/ebs/data/nrel/resstock/res_2024_amy2018_2"
path_resstock_metadata              := path_resstock_release + "/metadata"
# Base dir for gas mapper; script appends state=XX/utility_assignment.parquet
path_utility_assignment             := path_resstock_release + "/metadata_utility"
path_resstock_loads_00              := path_resstock_release + "/load_curve_hourly/state=" + state_upper + "/upgrade=" + upgrade
# TODO: replace RI placeholder with correct NY Cambium BA once confirmed
path_cambium                        := "s3://data.sb/nrel/cambium/2024/scenario=MidCase/t=2025/gea=ISONE/r=p133/data.parquet"
path_mc_table                       := env_var_or_default('MC_TABLE', path_config / "marginal_costs" / "ny_marginal_costs_2026_2035.csv")
path_s3_mc_output                   := "s3://data.sb/switchbox/marginal_costs/" + state + "/"
path_s3_utility_loads               := "s3://data.sb/eia/hourly_demand/utilities/"
path_electric_utility_stats         := "s3://data.sb/eia/861/electric_utility_stats/year=2024/state=" + state_upper + "/data.parquet"
path_default_tariff                 := path_tariffs_electric / default_tariff
path_default_supply_tariff          := path_tariffs_electric / default_tariff_supply
tou_tariff_key                      := utility + "_hp_seasonalTOU"
latest_output                       := path_repo / "utils/mid/latest_run_output.sh"

# =============================================================================
# VALIDATION
# =============================================================================

validate-config mode="warn":
  uv run python {{path_repo}}/utils/pre/validate_config.py \
    --scenario-config "{{path_scenario_config}}" \
    --state "{{state_upper}}" \
    --utility "{{utility}}" \
    --upgrade "{{upgrade}}" \
    --year "{{year}}" \
    --path-td-mc "{{path_td_mc}}" \
    --path-cambium "{{path_cambium}}" \
    --path-electric-utility-stats "{{path_electric_utility_stats}}" \
    --path-resstock-loads "{{path_resstock_loads_00}}" \
    {{ if mode == "strict" { "--strict" } else { "" } }}

# =============================================================================
# PRE-CONFIG: generate before any runs
# =============================================================================

create-scenario-yamls:
    just -f {{path_repo}}/utils/Justfile create-scenario-yamls

create-electric-tariff-maps-all:
    just -f {{path_repo}}/utils/Justfile write-tariff-maps-all "{{path_scenarios}}"

create-revenue-yaml:
  uv run python {{path_repo}}/utils/pre/create_revenue_yaml.py \
    --path-revenue-csv "{{path_revenue_csv}}" \
    --path-output-yaml "{{path_default_rev_requirement}}" \
    --utility "{{utility}}" \
    --year "{{year}}" \
    --utility-col "{{revenue_utility_col}}" \
    --year-col "{{revenue_year_col}}"

create-flat-tariff:
  just -f {{path_repo}}/utils/Justfile create-flat-tariff \
    "{{path_default_tariff}}" \
    "{{path_default_supply_tariff}}" \
    "{{utility}}_flat" \
    "{{utility}}_flat_supply" \
    "{{path_tariffs_electric}}"

create-seasonal-tou key=tou_tariff_key reference_tariff="" run_dir="":
  just -f {{path_repo}}/utils/Justfile create-seasonal-tou \
    "{{path_cambium}}" \
    "{{state_upper}}" \
    "{{utility}}" \
    "{{year}}" \
    "{{path_td_mc}}" \
    "{{path_resstock_release}}/metadata/state={{state_upper}}/upgrade={{upgrade}}/metadata-sb.parquet" \
    "{{path_resstock_release}}" \
    "{{upgrade}}" \
    "{{path_electric_utility_stats}}" \
    "{{key}}" \
    "{{path_config}}" \
    "{{path_periods_yaml}}" \
    "{{reference_tariff}}" \
    "{{run_dir}}"

create-gas-tariff-map electric_utility upgrade_id output_dir:
  just -f {{path_repo}}/utils/Justfile create-gas-tariff-map \
    "{{path_resstock_metadata}}" \
    "{{path_utility_assignment}}" \
    "{{state_upper}}" \
    "{{electric_utility}}" \
    "{{upgrade_id}}" \
    "{{output_dir}}"

create-gas-tariff-maps-all:
  just create-gas-tariff-map {{utility}} {{upgrade}} {{path_tariff_maps}}/gas

create-td-mc-data:
  uv run python {{path_repo}}/utils/pre/generate_utility_tx_dx_mc.py \
    --state {{state_upper}} \
    --utility {{utility}} \
    --mc-year {{mc_year}} \
    --load-year {{year}} \
    --mc-table-path {{path_mc_table}} \
    --utility-load-s3-base {{path_s3_utility_loads}} \
    --output-s3-base {{path_s3_mc_output}} \
    --upstream-hours {{upstream_hours}} \
    --dist-hours {{dist_hours}} \
    --upload

create-td-mc-data-all:
  just create-td-mc-data nyseg
  just create-td-mc-data rge
  just create-td-mc-data cenhud

# =============================================================================
# MID-CONFIG: generate between runs (using outputs from earlier runs)
# =============================================================================

compute-subclass-rr run_dir scenario_config run_num="1" group_col="has_hp" cross_subsidy_col="BAT_percustomer" differentiated_yaml=path_differentiated_rev_requirement default_yaml=path_default_rev_requirement:
  just -f {{path_repo}}/utils/Justfile compute-subclass-rr \
    "{{run_dir}}" \
    "{{scenario_config}}" \
    "{{differentiated_yaml}}" \
    "{{default_yaml}}" \
    "{{run_num}}" \
    "{{group_col}}" \
    "{{cross_subsidy_col}}"

compute-seasonal-discount-inputs run_dir resstock_base state upgrade cross_subsidy_col="BAT_percustomer" output_dir="":
  just -f {{path_repo}}/utils/Justfile compute-seasonal-discount-inputs \
    "{{run_dir}}" \
    "{{resstock_base}}" \
    "{{state_upper}}" \
    "{{upgrade}}" \
    "{{cross_subsidy_col}}" \
    "{{output_dir}}"

create-seasonal-discount-tariff base_tariff_json seasonal_inputs_csv label output_path periods_yaml=path_periods_yaml:
  just -f {{path_repo}}/utils/Justfile create-seasonal-discount-tariff \
    "{{base_tariff_json}}" \
    "{{seasonal_inputs_csv}}" \
    "{{label}}" \
    "{{output_path}}" \
    "{{periods_yaml}}"

copy-calibrated-tariff-from-run run_dir output_dir="":
  just -f {{path_repo}}/utils/Justfile copy-calibrated-tariff-from-run \
    "{{run_dir}}" \
    "{{state}}" \
    "{{output_dir}}"

compute-rev-requirements:
    #!/usr/bin/env bash
    set -euo pipefail
    run1_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 1)
    echo ">> compute-rev-requirements: resolved run-1 output → ${run1_dir}" >&2
    just compute-subclass-rr "${run1_dir}" "{{path_scenario_config}}"

# =============================================================================
# RUNS: scenario execution (run-1 through run-12)
# =============================================================================

run-scenario run_num:
  uv run python {{path_project}}/run_scenario.py \
    --run-num "{{run_num}}" \
    --utility "{{utility}}" \
    {{ if env_var_or_default('RDP_NUM_WORKERS', '') != '' { "--num-workers " + env_var_or_default('RDP_NUM_WORKERS', '') } else { "" } }}

# Run 1: precalc flat, delivery
run-1:
    just create-flat-tariff
    just run-scenario 1

# Run 2: precalc flat, supply (flat_supply.json exists from run-1 PRE)
run-2:
    just run-scenario 2

# Run 3: default flat calibrated, delivery (needs run 1 output)
run-3:
    #!/usr/bin/env bash
    set -euo pipefail
    run1_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 1)
    echo ">> run-3: resolved run-1 output → ${run1_dir}" >&2
    just copy-calibrated-tariff-from-run "${run1_dir}"
    just run-scenario 3

# Run 4: default flat calibrated, supply (needs run 2 output)
run-4:
    #!/usr/bin/env bash
    set -euo pipefail
    run2_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 2)
    echo ">> run-4: resolved run-2 output → ${run2_dir}" >&2
    just copy-calibrated-tariff-from-run "${run2_dir}"
    just run-scenario 4

# Run 5: precalc hp_seasonal vs flat, delivery
run-5:
    #!/usr/bin/env bash
    set -euo pipefail
    run1_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 1)
    echo ">> run-5: resolved run-1 output → ${run1_dir}" >&2
    just compute-seasonal-discount-inputs "${run1_dir}" "{{path_resstock_release}}" "{{state_upper}}" "{{upgrade}}"
    just create-seasonal-discount-tariff \
        "{{path_tariffs_electric}}/{{utility}}_flat_calibrated.json" \
        "${run1_dir}/seasonal_discount_rate_inputs.csv" \
        {{utility}}_hp_seasonal \
        "{{path_tariffs_electric}}/{{utility}}_hp_seasonal.json"
    just run-scenario 5

# Run 6: precalc hp_seasonal vs flat, supply
run-6:
    #!/usr/bin/env bash
    set -euo pipefail
    run2_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 2)
    echo ">> run-6: resolved run-2 output → ${run2_dir}" >&2
    just compute-seasonal-discount-inputs "${run2_dir}" "{{path_resstock_release}}" "{{state_upper}}" "{{upgrade}}"
    just create-seasonal-discount-tariff \
        "{{path_tariffs_electric}}/{{utility}}_flat_supply_calibrated.json" \
        "${run2_dir}/seasonal_discount_rate_inputs.csv" \
        {{utility}}_hp_seasonal_supply \
        "{{path_tariffs_electric}}/{{utility}}_hp_seasonal_supply.json"
    just run-scenario 6

# Run 7: default hp_seasonal calibrated, delivery (needs run 5 output)
run-7:
    #!/usr/bin/env bash
    set -euo pipefail
    run5_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 5)
    echo ">> run-7: resolved run-5 output → ${run5_dir}" >&2
    just copy-calibrated-tariff-from-run "${run5_dir}"
    just run-scenario 7

# Run 8: default hp_seasonal calibrated, supply (needs run 6 output)
run-8:
    #!/usr/bin/env bash
    set -euo pipefail
    run6_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 6)
    echo ">> run-8: resolved run-6 output → ${run6_dir}" >&2
    just copy-calibrated-tariff-from-run "${run6_dir}"
    just run-scenario 8

# Run 9: precalc hp_seasonalTOU vs flat, delivery
# Uses run-1 output for calibrated tariff (reference) and run-dir (building list/weights).
run-9:
    #!/usr/bin/env bash
    set -euo pipefail
    run1_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 1)
    echo ">> run-9: run-1 output → ${run1_dir}" >&2
    ref_tariff="{{path_tariffs_electric}}/{{utility}}_flat_calibrated.json"
    if [ ! -f "${ref_tariff}" ]; then
        echo ">> run-9: calibrated tariff missing, copying from run-1" >&2
        just copy-calibrated-tariff-from-run "${run1_dir}"
    fi
    just create-seasonal-tou {{tou_tariff_key}} "${ref_tariff}" "${run1_dir}"
    just run-scenario 9

# Run 10: precalc hp_seasonalTOU vs flat, supply
# Uses run-2 output for calibrated tariff (reference) and run-dir (building list/weights).
run-10:
    #!/usr/bin/env bash
    set -euo pipefail
    run2_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 2)
    echo ">> run-10: run-2 output → ${run2_dir}" >&2
    ref_tariff="{{path_tariffs_electric}}/{{utility}}_flat_supply_calibrated.json"
    if [ ! -f "${ref_tariff}" ]; then
        echo ">> run-10: calibrated supply tariff missing, copying from run-2" >&2
        just copy-calibrated-tariff-from-run "${run2_dir}"
    fi
    just create-seasonal-tou {{tou_tariff_key}}_supply "${ref_tariff}" "${run2_dir}"
    just run-scenario 10

# Run 11: default hp_seasonalTOU calibrated, delivery (needs run 9 output)
run-11:
    #!/usr/bin/env bash
    set -euo pipefail
    run9_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 9)
    echo ">> run-11: resolved run-9 output → ${run9_dir}" >&2
    just copy-calibrated-tariff-from-run "${run9_dir}"
    just run-scenario 11

# Run 12: default hp_seasonalTOU calibrated, supply (needs run 10 output)
run-12:
    #!/usr/bin/env bash
    set -euo pipefail
    run10_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 10)
    echo ">> run-12: resolved run-10 output → ${run10_dir}" >&2
    just copy-calibrated-tariff-from-run "${run10_dir}"
    just run-scenario 12

# Run 13: precalc hp_seasonalTOU_flex vs flat, delivery
# Reuses run-9 TOU tariff shape; _flex differs only by runtime elasticity.
run-13:
    #!/usr/bin/env bash
    set -euo pipefail
    source_tou="{{path_tariffs_electric}}/{{tou_tariff_key}}.json"
    target_tou="{{path_tariffs_electric}}/{{tou_tariff_key}}_flex.json"
    if [ ! -f "${source_tou}" ]; then
        echo ">> run-13: missing source TOU tariff: ${source_tou}" >&2
        echo ">> run-13: run run-9 first to generate it." >&2
        exit 1
    fi
    cp "${source_tou}" "${target_tou}"
    echo ">> run-13: copied ${source_tou} -> ${target_tou}" >&2
    just run-scenario 13

# Run 14: precalc hp_seasonalTOU_flex vs flat, supply
# Reuses run-10 TOU supply tariff shape; _flex differs only by runtime elasticity.
run-14:
    #!/usr/bin/env bash
    set -euo pipefail
    source_tou="{{path_tariffs_electric}}/{{tou_tariff_key}}_supply.json"
    target_tou="{{path_tariffs_electric}}/{{tou_tariff_key}}_flex_supply.json"
    if [ ! -f "${source_tou}" ]; then
        echo ">> run-14: missing source TOU supply tariff: ${source_tou}" >&2
        echo ">> run-14: run run-10 first to generate it." >&2
        exit 1
    fi
    cp "${source_tou}" "${target_tou}"
    echo ">> run-14: copied ${source_tou} -> ${target_tou}" >&2
    just run-scenario 14

# Run 15: default hp_seasonalTOU_flex calibrated, delivery (needs run 13 output)
run-15:
    #!/usr/bin/env bash
    set -euo pipefail
    run13_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 13)
    echo ">> run-15: resolved run-13 output -> ${run13_dir}" >&2
    just copy-calibrated-tariff-from-run "${run13_dir}"
    just run-scenario 15

# Run 16: default hp_seasonalTOU_flex calibrated, supply (needs run 14 output)
run-16:
    #!/usr/bin/env bash
    set -euo pipefail
    run14_dir=$(bash "{{latest_output}}" "{{path_scenario_config}}" 14)
    echo ">> run-16: resolved run-14 output -> ${run14_dir}" >&2
    just copy-calibrated-tariff-from-run "${run14_dir}"
    just run-scenario 16

# =============================================================================
# MASTER: orchestration entrypoints
# =============================================================================

all-pre:
    just create-scenario-yamls
    just create-electric-tariff-maps-all
    just create-gas-tariff-maps-all
    just create-revenue-yaml
    just validate-config

run-all-sequential:
    just run-1
    just compute-rev-requirements
    just run-2
    just run-3
    just run-4
    just run-5
    just run-6
    just run-7
    just run-8
    just run-9
    just run-10
    just run-11
    just run-12
    just run-13
    just run-14
    just run-15
    just run-16

# Run all 16 scenarios as 8 delivery/supply waves in parallel.
# Each wave runs the odd (delivery) and even (supply) run concurrently, each with
# half the available CPUs. Requires --num-workers support in run_scenario.py.
# Use when T4/T8 ratio r < 1.8 (measured in cairo_parallelism_and_workers.md).
run-all-parallel-tracks:
    #!/usr/bin/env bash
    set -euo pipefail
    WORKERS=$(( $(nproc) / 2 ))
    [ "$WORKERS" -lt 1 ] && WORKERS=1
    export RDP_NUM_WORKERS="$WORKERS"
    echo ">> run-all-parallel-tracks: ${WORKERS} workers/track on $(nproc) total vCPUs" >&2

    # Pre-Wave 1: create shared flat tariff files (run-1's PRE step, needed by run-2)
    just create-flat-tariff

    # Wave 1: run-1 (delivery) and run-2 (supply) scenario steps, now truly concurrent
    just run-scenario 1 & PID1=$!
    just run-scenario 2 & PID2=$!
    wait $PID1 || { echo "ERROR: run-1 failed" >&2; exit 1; }
    wait $PID2 || { echo "ERROR: run-2 failed" >&2; exit 1; }

    # compute-rev-requirements uses run-1 BAT output; needed by runs 5, 6, 9, 10
    just compute-rev-requirements

    # Wave 2: run-3 (copies calibrated tariff from run-1) and run-4 (from run-2)
    just run-3 & PID3=$!
    just run-4 & PID4=$!
    wait $PID3 || { echo "ERROR: run-3 failed" >&2; exit 1; }
    wait $PID4 || { echo "ERROR: run-4 failed" >&2; exit 1; }

    # Wave 3: run-5 (delivery hp_seasonal) and run-6 (supply hp_seasonal)
    just run-5 & PID5=$!
    just run-6 & PID6=$!
    wait $PID5 || { echo "ERROR: run-5 failed" >&2; exit 1; }
    wait $PID6 || { echo "ERROR: run-6 failed" >&2; exit 1; }

    # Wave 4: run-7 (default hp_seasonal calibrated, delivery) and run-8 (default hp_seasonal calibrated, supply)
    just run-7 & PID7=$!
    just run-8 & PID8=$!
    wait $PID7 || { echo "ERROR: run-7 failed" >&2; exit 1; }
    wait $PID8 || { echo "ERROR: run-8 failed" >&2; exit 1; }

    # Wave 5: run-9 (delivery TOU) and run-10 (supply TOU); also depend on compute-rev-requirements
    just run-9 & PID9=$!
    just run-10 & PID10=$!
    wait $PID9 || { echo "ERROR: run-9 failed" >&2; exit 1; }
    wait $PID10 || { echo "ERROR: run-10 failed" >&2; exit 1; }

    # Wave 6: run-11 (default hp_seasonalTOU calibrated, delivery) and run-12 (default hp_seasonalTOU calibrated, supply)
    just run-11 & PID11=$!
    just run-12 & PID12=$!
    wait $PID11 || { echo "ERROR: run-11 failed" >&2; exit 1; }
    wait $PID12 || { echo "ERROR: run-12 failed" >&2; exit 1; }

    # Wave 7: run-13 (precalc hp_seasonalTOU_flex, delivery) and run-14 (precalc hp_seasonalTOU_flex, supply)
    just run-13 & PID13=$!
    just run-14 & PID14=$!
    wait $PID13 || { echo "ERROR: run-13 failed" >&2; exit 1; }
    wait $PID14 || { echo "ERROR: run-14 failed" >&2; exit 1; }

    # Wave 8: run-15 (default hp_seasonalTOU_flex calibrated, delivery) and run-16 (default hp_seasonalTOU_flex calibrated, supply)
    just run-15 & PID15=$!
    just run-16 & PID16=$!
    wait $PID15 || { echo "ERROR: run-15 failed" >&2; exit 1; }
    wait $PID16 || { echo "ERROR: run-16 failed" >&2; exit 1; }

    echo ">> run-all-parallel-tracks: all 16 runs complete" >&2

# Run all 7 utilities sequentially.
run-all-utilities-sequential:
    #!/usr/bin/env bash
    set -euo pipefail
    for util in coned nimo nyseg cenhud or psegli rge; do
        echo ">> ======================================" >&2
        echo ">> Running utility: $util" >&2
        echo ">> ======================================" >&2
        UTILITY="$util" just all-pre
        UTILITY="$util" just run-all-sequential
    done

# Parallel multi-utility execution is not yet implemented. Each utility's
# outputs are utility-namespaced (config/tariffs/electric/<utility>_*.json,
# S3 output dirs include utility name), so parallel should be safe -- but
# needs testing before enabling.

# =============================================================================
# DATA PREP: tariff mapping, polygon downloads, marginal costs
# (separate concern from run orchestration; run before all-pre)
# =============================================================================

# --- Electric tariff mapping ---

map-electric-tariff electric_utility SB_scenario_type SB_scenario_year upgrade_id output_dir:
  uv run python {{path_repo}}/utils/pre/electric_tariff_mapper.py \
    --metadata_path "{{path_resstock_release}}" \
    --state {{state_upper}} \
    --upgrade_id "{{upgrade_id}}" \
    --electric_utility "{{electric_utility}}" \
    --SB_scenario_type "{{SB_scenario_type}}" \
    --SB_scenario_year "{{SB_scenario_year}}" \
    --output_dir "{{output_dir}}"

map-electric-coned-default:
  just map-electric-tariff coned default 1 00 {{path_tariff_maps}}/electric

map-electric-coned-seasonal:
  just map-electric-tariff coned seasonal 1 00 {{path_tariff_maps}}/electric

map-electric-coned-class-specific-seasonal:
  just map-electric-tariff coned class_specific_seasonal 1 00 {{path_tariff_maps}}/electric

map-electric-national-grid-default:
  just map-electric-tariff nimo default 1 00 {{path_tariff_maps}}/electric

map-electric-national-grid-seasonal:
  just map-electric-tariff nimo seasonal 1 00 {{path_tariff_maps}}/electric

map-electric-national-grid-class-specific-seasonal:
  just map-electric-tariff nimo class_specific_seasonal 1 00 {{path_tariff_maps}}/electric

map-electric-nyseg-default:
  just map-electric-tariff nyseg default 1 00 {{path_tariff_maps}}/electric

map-electric-nyseg-seasonal:
  just map-electric-tariff nyseg seasonal 1 00 {{path_tariff_maps}}/electric

map-electric-nyseg-class-specific-seasonal:
  just map-electric-tariff nyseg class_specific_seasonal 1 00 {{path_tariff_maps}}/electric

# --- Gas tariff mapping ---

map-gas-tariff electric_utility upgrade_id output_dir:
  just create-gas-tariff-map "{{electric_utility}}" "{{upgrade_id}}" "{{output_dir}}"

map-gas-bath:
  just map-gas-tariff bath 00 {{path_tariff_maps}}/gas

map-gas-cenhud:
  just map-gas-tariff cenhud 00 {{path_tariff_maps}}/gas

map-gas-chautauqua:
  just map-gas-tariff chautauqua 00 {{path_tariff_maps}}/gas

map-gas-coned:
  just map-gas-tariff coned 00 {{path_tariff_maps}}/gas

map-gas-national-grid:
  just map-gas-tariff nimo 00 {{path_tariff_maps}}/gas

map-gas-nyseg:
  just map-gas-tariff nyseg 00 {{path_tariff_maps}}/gas

map-gas-or:
  just map-gas-tariff or 00 {{path_tariff_maps}}/gas

map-gas-psegli:
  just map-gas-tariff psegli 00 {{path_tariff_maps}}/gas

map-gas-rge:
  just map-gas-tariff rge 00 {{path_tariff_maps}}/gas

map-gas-all-utilities:
  just map-gas-bath
  just map-gas-cenhud
  just map-gas-chautauqua
  just map-gas-coned
  just map-gas-national-grid
  just map-gas-nyseg
  just map-gas-or
  just map-gas-psegli
  just map-gas-rge

# --- NY polygon downloads ---

download-ny-utility-polygons:
	just download-ny-electric-utility-polygons
	just download-ny-gas-utility-polygons

download-ny-electric-utility-polygons:
	wget -qO- --user-agent="Mozilla/5.0 (Windows NT 10.0; Win64; x64; rv:89.0) Gecko/20100101 Firefox/89.0" "https://data.ny.gov/resource/awza-4vgu.csv" | aws s3 cp - "s3://data.sb/gis/utility_boundaries/ny_electric_utilities_`date +%Y%m%d`.csv"
	just test-ny-electric-utility-polygons-download

download-ny-gas-utility-polygons:
	wget -qO- --user-agent="Mozilla/5.0 (Windows NT 10.0; Win64; x64; rv:89.0) Gecko/20100101 Firefox/89.0" "https://data.ny.gov/api/views/i7nm-ih89/rows.csv" | aws s3 cp - "s3://data.sb/gis/utility_boundaries/ny_gas_utilities_$(date +%Y%m%d).csv"
	just test-ny-gas-utility-polygons-download

test-ny-electric-utility-polygons-download:
	aws s3 ls "s3://data.sb/gis/utility_boundaries/" | grep "ny_electric_utilities_.*\.csv"

test-ny-gas-utility-polygons-download:
	aws s3 ls "s3://data.sb/gis/utility_boundaries/" | grep "ny_gas_utilities_.*\.csv"

# =============================================================================
# POST: after all runs are completed
# =============================================================================

apply-ny-lmi-discounts run_dir utility fpl_year cpi_s3_path participation_rate="1.0" rider="false":
  uv run python {{path_repo}}/utils/post/apply_ny_lmi_discounts_to_bills.py \
    --run-dir "{{run_dir}}" \
    --state {{state_upper}} \
    --utility {{utility}} \
    --fpl-year {{fpl_year}} \
    --cpi-s3-path "{{cpi_s3_path}}" \
    --participation-rate {{participation_rate}} \
    {{ if rider == "true" { "--rider" } else { "" } }} \
    --upload

# -----------------------------------------------------------------------------
# PRE: Marginal cost allocation (utility loads and MC table → S3; data assumed on S3)
# -----------------------------------------------------------------------------
state_data_prep := "NY"
load_year_data_prep := "2025"
mc_year_data_prep := "2026"
utility_s3_base_data_prep := "s3://data.sb/eia/hourly_demand/utilities/"
mc_output_s3_base_data_prep := "s3://data.sb/switchbox/marginal_costs/ny/"

create-td-mc-data-legacy utility:
  cd {{project_root}} && uv run python {{project_root}}/utils/pre/generate_utility_tx_dx_mc.py \
    --state {{state_data_prep}} \
    --utility {{utility}} \
    --mc-year {{mc_year_data_prep}} \
    --load-year {{load_year_data_prep}} \
    --mc-table-path {{project_root}}/rate_design/ny/hp_rates/config/marginal_costs/ny_marginal_costs_2026_2035.csv \
    --utility-load-s3-base {{utility_s3_base_data_prep}} \
    --output-s3-base {{mc_output_s3_base_data_prep}} \
    --upload

create-td-mc-data-all-legacy:
  just create-td-mc-data-legacy nyseg
  just create-td-mc-data-legacy rge
  just create-td-mc-data-legacy cenhud

create-td-mc-data-nyseg:
  just create-td-mc-data-legacy nyseg

create-td-mc-data-rge:
  just create-td-mc-data-legacy rge

create-td-mc-data-cenhud:
  just create-td-mc-data-legacy cenhud

# Supply marginal costs (LBMP + ICAP MCOS)
supply_mc_year := "2025"
zone_mapping_path := "s3://data.sb/nyiso/zone_mapping/ny_utility_zone_mapping.csv"

create-supply-mc-data utility:
  cd {{project_root}} && uv run python {{project_root}}/utils/pre/generate_utility_supply_mc.py \
    --utility {{utility}} \
    --year {{supply_mc_year}} \
    --zone-mapping-path {{zone_mapping_path}} \
    --upload

create-supply-mc-data-all:
  just create-supply-mc-data cenhud
  just create-supply-mc-data coned
  just create-supply-mc-data nimo
  just create-supply-mc-data nyseg
  just create-supply-mc-data or
  just create-supply-mc-data rge
  just create-supply-mc-data psegli

# =============================================================================
# Create scenario YAMLs from Google Sheets
# =============================================================================

# Create scenario YAMLs from the Runs & Charts Google Sheet (requires G_* env and browser auth)
# Writes rate_design/<state>/hp_rates/config/scenarios_<utility>.yaml per utility
create-scenario-yamls-legacy:
    uv run python {{project_root}}/utils/pre/create_scenario_yamls.py

# =============================================================================
# PRE: Write electric tariff map CSVs from scenario YAMLs
# =============================================================================

# Write electric tariff map CSVs for all runs in a single scenario file (pre-step; run before run_scenario)
write-tariff-maps scenario_config:
  uv run python {{project_root}}/utils/pre/write_tariff_maps_from_scenario.py \
    --scenario-config "{{scenario_config}}"

# Write electric tariff map CSVs for every utility scenario YAML in config/scenarios/
write-tariff-maps-all:
  for f in {{project_root}}/rate_design/ny/hp_rates/config/scenarios/scenarios_*.yaml; do \
    uv run python {{project_root}}/utils/pre/write_tariff_maps_from_scenario.py \
      --scenario-config "$f"; \
  done

# Gas tariff fetch: just -f rate_design/ny/hp_rates/config/tariffs/gas/Justfile fetch-gas
# Electric Genability tariff fetch: just -f rate_design/ny/hp_rates/config/tariffs/electric/Justfile fetch-genability
