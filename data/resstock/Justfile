# ResStock metadata pipeline: download, identify HP customers, heating type, assign utilities (NY).
#
# Usage (from project root):
#   just -f data/resstock/Justfile fetch NY
#   just -f data/resstock/Justfile fetch RI
#   just -f data/resstock/Justfile test-download NY
#   just -f data/resstock/Justfile resstock-identify-hp-customers NY
#   just -f data/resstock/Justfile resstock-identify-hp-customers RI
#   just -f data/resstock/Justfile assign-utility-ny res_2024_amy2018_2 00

path_local_repo := `git rev-parse --show-toplevel`
resstock_release := "res_2024_amy2018_2"
resstock_upgrade_ids := "0 1 2 3 4 5"
resstock_upgrade_ids_padded := "00 01 02 03 04 05"
path_s3_parquet := "s3://data.sb/nrel/resstock"
path_local_parquet := "/data.sb/nrel/resstock"

# Download ResStock metadata and loads via bsf. Pass state: NY or RI (or both as space-separated).
fetch state:
    #!/usr/bin/env bash
    s="{{state}}"
    if [[ -z "$s" ]]; then
        echo "Usage: just fetch <state>"
        echo "Example: just fetch NY"
        exit 1
    fi
    cd "{{path_local_repo}}" && uv run bsf \
        --product resstock \
        --release_year 2024 \
        --weather_file amy2018 \
        --release_version 2 \
        --states $s \
        --file_type "metadata load_curve_hourly load_curve_annual" \
        --upgrade_id "{{resstock_upgrade_ids}}" \
        --output_directory {{path_local_parquet}} \
        --sample 0

fetch-load-curve-annual state:
    #!/usr/bin/env bash
    s="{{state}}"
    if [[ -z "$s" ]]; then
        echo "Usage: just fetch-load-curve-annual <state>"
        echo "Example: just fetch-load-curve-annual NY"
        exit 1
    fi
    cd "{{path_local_repo}}" && uv run bsf \
        --product resstock \
        --release_year 2024 \
        --weather_file amy2018 \
        --release_version 2 \
        --states $s \
        --file_type "load_curve_annual" \
        --upgrade_id "{{resstock_upgrade_ids}}" \
        --output_directory {{path_local_parquet}} \
        --sample 0


# Copy resstock data from one release to another.
copy-resstock-data release_from release_to state upgrade_ids file_types:
	uv run python "{{path_local_repo}}/utils/pre/copy_resstock_data.py" \
		--data_path "{{path_s3_parquet}}" \
		--release_from "{{release_from}}" \
		--release_to "{{release_to}}" \
		--state "{{state}}" \
		--upgrade_ids "{{upgrade_ids}}" \
		--file_types "{{file_types}}"

copy-resstock-data-2024-amy2018-2-NY upgrade_ids file_types:
	just copy-resstock-data res_2024_amy2018_2 res_2024_amy2018_2_sb NY "{{upgrade_ids}}" "{{file_types}}"

copy-resstock-data-2024-amy2018-2-RI upgrade_ids file_types:
	just copy-resstock-data res_2024_amy2018_2 res_2024_amy2018_2_sb RI "{{upgrade_ids}}" "{{file_types}}"

# Verify ResStock download. Pass state (NY or RI).
test-download state:
    cd "{{path_local_repo}}" && uv run python "{{path_local_repo}}/tests/test_resstock_download_to_s3.py" \
        --data_path "{{path_s3_parquet}}/" \
        --release {{resstock_release}} \
        --state {{state}} \
        --upgrade_id {{resstock_upgrade_ids_padded}}

# Identify HP customers and heating type for a state. Pass state (NY or RI).
identify-hp-customers release state upgrade_ids input_filename output_filename:
    #!/usr/bin/env bash
    set -e
    if [[ "{{release}}" != *"2024"* ]]; then
        echo "Warning: this task is only verified to work with 2024 releases."
    fi
    for upgrade_id in {{upgrade_ids}}; do
        uv run python "{{path_local_repo}}/data/resstock/identify_hp_customers.py" \
            --input_dir "{{path_s3_parquet}}/{{release}}/metadata/state={{state}}/upgrade=$upgrade_id" \
            --output_dir "{{path_s3_parquet}}/{{release}}/metadata/state={{state}}/upgrade=$upgrade_id" \
            --input_filename "{{input_filename}}" \
            --output_filename "{{output_filename}}" \
            --upgrade_id "$upgrade_id"
    done

identify-heating-type release state upgrade_ids input_filename output_filename:
    #!/usr/bin/env bash
    set -e
    if [[ "{{release}}" != *"2024"* ]]; then
        echo "Warning: this task is only verified to work with 2024 releases."
    fi
    for upgrade_id in {{upgrade_ids}}; do
        uv run python "{{path_local_repo}}/data/resstock/identify_heating_type.py" \
            --input_dir "{{path_s3_parquet}}/{{release}}/metadata/state={{state}}/upgrade=$upgrade_id" \
            --output_dir "{{path_s3_parquet}}/{{release}}/metadata/state={{state}}/upgrade=$upgrade_id" \
            --input_filename "{{input_filename}}" \
            --output_filename "{{output_filename}}" \
            --upgrade_id "$upgrade_id"
    done

identify-natgas-connection release state upgrade_ids input_metadata_filename output_filename:
    #!/usr/bin/env bash
    set -e
    if [[ "{{release}}" != *"2024"* ]]; then
        echo "Warning: this task is only verified to work with 2024 releases."
    fi
    for upgrade_id in {{upgrade_ids}}; do
        uv run python "{{path_local_repo}}/data/resstock/identify_natgas_connection.py" \
            --input_metadata_dir "{{path_s3_parquet}}/{{release}}/metadata/state={{state}}/upgrade=$upgrade_id" \
            --input_load_curve_annual_dir "{{path_s3_parquet}}/{{release}}/load_curve_annual/state={{state}}/upgrade=$upgrade_id" \
            --output_dir "{{path_s3_parquet}}/{{release}}/metadata/state={{state}}/upgrade=$upgrade_id" \
            --input_metadata_filename "{{input_metadata_filename}}" \
            --input_load_curve_annual_filename "{{state}}_upgrade"$upgrade_id"_metadata_and_annual_results.parquet" \
            --output_filename "{{output_filename}}"
    done

identify-hp-customers-all-upgrades state:
    just identify-hp-customers {{resstock_release}} {{state}} "{{resstock_upgrade_ids_padded}}" "metadata.parquet" "metadata-sb.parquet"

identify-heating-type-all-upgrades state:
    just identify-heating-type {{resstock_release}} {{state}} "{{resstock_upgrade_ids_padded}}" "metadata.parquet" "metadata-sb.parquet"

identify-natgas-connection-all-upgrades state:
    just identify-natgas-connection {{resstock_release}} {{state}} "{{resstock_upgrade_ids_padded}}" "metadata.parquet" "metadata-sb.parquet"

identify-hp-and-heating-type-all-upgrades-and-natgas-connection state:
    just identify-hp-customers {{resstock_release}} {{state}} "{{resstock_upgrade_ids_padded}}" "metadata.parquet" "metadata-sb.parquet"
    just identify-heating-type {{resstock_release}} {{state}} "{{resstock_upgrade_ids_padded}}" "metadata-sb.parquet" "metadata-sb.parquet"
    just identify-natgas-connection {{resstock_release}} {{state}} "{{resstock_upgrade_ids_padded}}" "metadata-sb.parquet" "metadata-sb.parquet"

# Identify HP customers for a state (shortcut).
resstock-identify-hp-customers state:
    just identify-hp-customers-all-upgrades {{state}}

# Add vulnerability columns (random placeholder) to metadata-sb.parquet.
# Safe to re-run: drops pre-existing vulnerability columns before adding.
add-vulnerability-columns release state upgrade_ids input_filename output_filename:
    #!/usr/bin/env bash
    set -e
    for upgrade_id in {{upgrade_ids}}; do
        uv run python "{{path_local_repo}}/data/resstock/add_vulnerability_columns.py" \
            --input_dir "{{path_s3_parquet}}/{{release}}/metadata/state={{state}}/upgrade=$upgrade_id" \
            --output_dir "{{path_s3_parquet}}/{{release}}/metadata/state={{state}}/upgrade=$upgrade_id" \
            --input_filename "{{input_filename}}" \
            --output_filename "{{output_filename}}" \
            --upgrade_id "$upgrade_id"
    done

add-vulnerability-columns-all-upgrades state:
    just add-vulnerability-columns {{resstock_release}} {{state}} "{{resstock_upgrade_ids_padded}}" "metadata-sb.parquet" "metadata-sb.parquet"

# Full NY metadata pipeline: HP customers → heating type → vulnerability columns.
# Pass lmi="false" to skip vulnerability (e.g. for runs that don't need LMI discounts).
prepare-metadata-ny lmi="true":
    just identify-hp-customers {{resstock_release}} NY "{{resstock_upgrade_ids_padded}}" "metadata.parquet" "metadata-sb.parquet"
    just identify-heating-type {{resstock_release}} NY "{{resstock_upgrade_ids_padded}}" "metadata-sb.parquet" "metadata-sb.parquet"
    {{ if lmi == "true" { "just add-vulnerability-columns " + resstock_release + " NY \"" + resstock_upgrade_ids_padded + "\" \"metadata-sb.parquet\" \"metadata-sb.parquet\"" } else { "echo 'Skipping vulnerability columns (lmi=false)'" } }}

# =============================================================================
# NY-only: polygon downloads (data prep for utility assignment)
# =============================================================================

download-ny-utility-polygons:
	just download-ny-electric-utility-polygons
	just download-ny-gas-utility-polygons

download-ny-electric-utility-polygons:
	wget -qO- --user-agent="Mozilla/5.0 (Windows NT 10.0; Win64; x64; rv:89.0) Gecko/20100101 Firefox/89.0" "https://data.ny.gov/resource/awza-4vgu.csv" | aws s3 cp - "s3://data.sb/gis/utility_boundaries/ny_electric_utilities_`date +%Y%m%d`.csv"
	just test-ny-electric-utility-polygons-download

download-ny-gas-utility-polygons:
	wget -qO- --user-agent="Mozilla/5.0 (Windows NT 10.0; Win64; x64; rv:89.0) Gecko/20100101 Firefox/89.0" "https://data.ny.gov/api/views/i7nm-ih89/rows.csv" | aws s3 cp - "s3://data.sb/gis/utility_boundaries/ny_gas_utilities_$(date +%Y%m%d).csv"
	just test-ny-gas-utility-polygons-download

test-ny-electric-utility-polygons-download:
	aws s3 ls "s3://data.sb/gis/utility_boundaries/" | grep "ny_electric_utilities_.*\.csv"

test-ny-gas-utility-polygons-download:
	aws s3 ls "s3://data.sb/gis/utility_boundaries/" | grep "ny_gas_utilities_.*\.csv"

# Assign electric/gas utilities to ResStock buildings (NY only). Run download-ny-utility-polygons first.
assign-utility-ny release upgrade electric_poly gas_poly:
    uv run python "{{path_local_repo}}/data/resstock/assign_utility_ny.py" \
        --input_metadata_dir "{{path_s3_parquet}}/{{release}}/metadata/state=NY/upgrade={{upgrade}}/" \
        --input_metadata_filename "metadata-sb.parquet" \
        --output_metadata_dir "{{path_s3_parquet}}/{{release}}/metadata_utility/state=NY/" \
        --output_utility_assignment_filename "utility_assignment.parquet" \
        --electric_poly_dir "s3://data.sb/gis/utility_boundaries/" \
        --gas_poly_dir "s3://data.sb/gis/utility_boundaries/" \
        --electric_poly_filename "{{electric_poly}}" \
        --gas_poly_filename "{{gas_poly}}"

# Assign electric/gas utilities to ResStock buildings (RI only)
assign-utility-ri release upgrade:
    uv run python "{{path_local_repo}}/data/resstock/assign_utility_ri.py" \
        --input_metadata_dir "{{path_s3_parquet}}/{{release}}/metadata/state=RI/upgrade={{upgrade}}/" \
        --input_metadata_filename "metadata-sb.parquet" \
        --output_metadata_dir "{{path_s3_parquet}}/{{release}}/metadata_utility/state=RI/" \
        --output_utility_assignment_filename "utility_assignment.parquet" \

# Approximate non-HP load for a state
approximate-non-hp-load state upgrade_id input_release output_release k update_MF update_other_fuel_types:
    uv run python "{{path_local_repo}}/utils/pre/approximate_non_hp_load.py" \
        --path_s3 "{{path_s3_parquet}}" \
        --state "{{state}}" \
        --upgrade_id "{{upgrade_id}}" \
        --input_release "{{input_release}}" \
        --output_release "{{output_release}}" \
        --k "{{k}}" \
        --update_MF "{{update_MF}}" \
        --update_other_fuel_types "{{update_other_fuel_types}}"

approximate-non-hp-load-upgrade-02-NY:
    just copy-resstock-data-2024-amy2018-2-NY "00 02" "metadata metadata_utility load_curve_hourly" # Copy data to new sb release
    just approximate-non-hp-load NY 02 res_2024_amy2018_2 res_2024_amy2018_2_sb 15 True True # Approximate non-HP load for upgrade 02
    aws s3 sync s3://data.sb/nrel/resstock/res_2024_amy2018_2_sb/ /ebs/data/nrel/resstock/res_2024_amy2018_2_sb/


approximate-non-hp-load-upgrade-02-RI:
    just approximate-non-hp-load RI 02 res_2024_amy2018_2 res_2024_amy2018_2_sb 15 True True
