# =============================================================================
# â­ DEFAULT
# =============================================================================
project_root := `git rev-parse --show-toplevel`

# If you run `just`, you see all available commands
default:
    @just --list


# =============================================================================
# ğŸ” CODE QUALITY & TESTING
# =============================================================================
# These commands check your code quality and run tests

# Run code quality tools (same as CI)
check:
    echo "ğŸš€ Checking lock file consistency with 'pyproject.toml'"
    uv lock --locked
    echo "ğŸš€ Linting, formatting, and type checking code"
    prek run -a

# Check for obsolete dependencies
check-deps:
    echo "ğŸš€ Checking for obsolete dependencies: Running deptry"
    uv run deptry .

# Run tests
test:
    echo "ğŸš€ Testing code: Running pytest"
    uv run python -m pytest --doctest-modules tests/

# =============================================================================
# ğŸ—ï¸  DEVELOPMENT ENVIRONMENT SETUP
# =============================================================================
# These commands help you set up your development environment

# Install uv, python packages, r packages, prek, and pre-commit hooks
install:
    @echo "ğŸš€ Setting up development environment\n"
    @.devcontainer/install-python-deps.sh .
    @.devcontainer/install-prek.sh
    @.devcontainer/install-prek-deps.sh
    @echo "âœ¨ Development environment ready!\n"

# Clean generated files and caches
clean:
    rm -rf .pytest_cache .ruff_cache tmp

# =============================================================================
# ğŸ“Š DATA (FRED, etc.)
# =============================================================================

# Fetch CPIAUCSL from FRED (2020â€“2025) and upload to S3
# Output: s3://data.sb/fred/cpi/cpiaucsl_2020_2025_<YYYYMMDD>.parquet
# Requires FRED_API_KEY in environment
fetch-cpi start_year="2020" end_year="2025":
    uv run python {{project_root}}/utils/post/fetch_cpi_from_fred.py \
        --start-year {{start_year}} --end-year {{end_year}} --upload

# =============================================================================
# ğŸ” AWS
# =============================================================================

# Authenticate with AWS via SSO (for manual AWS CLI usage like S3 access)
# Automatically configures SSO if not already configured
aws:
    .devcontainer/devpod/aws.sh

# Launch devcontainer locally with Docker
up-local rebuild="":
    .devcontainer/devpod/up-local.sh {{ rebuild }}

# Launch devcontainer on AWS EC2, using the specified machine type
up-aws MACHINE_TYPE="t3.xlarge" rebuild="":
    .devcontainer/devpod/up-aws.sh {{ MACHINE_TYPE }} {{ rebuild }}

# Show active EC2 instances running devcontainers, and commands to delete them
up-aws-list:
    .devcontainer/devpod/up-aws-list.sh

# =============================================================================
# ğŸš€ DEVELOPMENT ENVIRONMENT
# =============================================================================

# Ensure Terraform is installed (internal dependency). Depends on aws so credentials
# are valid before any Terraform or infra script runs.
_terraform: aws
    bash infra/install-terraform.sh

# Set up EC2 instance (run once by admin)
# Idempotent: safe to run multiple times
dev-setup: _terraform
    bash infra/dev-setup.sh

# Destroy EC2 instance but preserve data volume (to recreate, run dev-setup again)
dev-teardown: _terraform
    bash infra/dev-teardown.sh

# Destroy everything including data volume (WARNING: destroys all data!)
dev-teardown-all: _terraform
    bash infra/dev-teardown-all.sh

# User login (run by any authorized user)
dev-login: aws
    bash infra/dev-login.sh
